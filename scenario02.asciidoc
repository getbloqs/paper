[scenario02]
== Szenario 2

[[section0201]]
=== Problemstellung: Domain Name System

Das Internet und alle darauf basierenden Dienste nutzen sog. IP-Adressen um Rechner im Netzwerk zu identifizieren. IP-Adressen sind mehrstellige Zahlenkombinationen und für Menschen schwer zu merken, sodass Domain-Namen eingeführt wurden um dies zu erleichtern. Ein Domain-Name ist ein leicht merkbarer Bezeichner, welcher quasi als Alias für eine IP-Adresse verwendet wird.

Das System, welches die Namensauflösung, also die Umwandlung von Domain-Name in eine IP-Adresse, ist das Domain Name System (DNS). DNS ist zwar grundsätzlich eine dezentrale Datenbank unterliegt jedoch zentraler Kontrolle. An der obersten Hierarchieebene steht dabei die Internet Corporation for Assigned Names and Numbers (ICANN), welche Top Level Domains (TLD) vergibt und die Verwaltung dieser an die jeweiligen regionalen Verwaltungen weiterreicht. Die regionalen Verwaltungen vertreiben die Domain-Namen dann über akkreditierte Registrare an Domain Käufer.

DNS basiert auf einer Serverinfrastruktur, welche Anfragen für die Übersetzung von Domain-Namen zu IP-Adressen vornehmen. Über die Kontrolle der DNS-Server können Domain-Namen in DNS-Zonen manipuliert und damit zB zensiert werden. So können zB Webseiten wie Wikipedia in Ländern wie der Türkei gesperrt werden.

Ein auf Blockchain-Technologie basierendes Domain Name System würde es ermöglichen nicht nur die Datenhaltung zu dezentralisieren, sonder auch die Kontrolle über die DNS-Einträge. So würde zB der DNS-Eintrag für Wikipedia nur vom Betreiber selbst verwaltet und verändert werden können.

[[section0202]]
=== Demonstration einer Subdomain Registry

Als Prototyp einer dezentralen Domain Registrierung wird dies anhand einer Demonstration mittels von Subdomain Redirects realisiert. Das "Eigentum" von Subdomains einer Toplevel Domain werden über einen Smart Contract verwaltet. Folgende Funktionen sind verfügbar:

 - Es können für einen Gegenwert, welcher in Ether bezahlt wird, Subdomains für einen Zeitraum registriert werden.
 - Der Eigentümer der Subdomain kann einen beliebigen Redirect Link hinterlegen. Bei Aufruf der Subdomain über einen Browser soll ein Redirect zum hinterlegten Link durchgeführt werden.
 - Nach einer gewissen Dauer soll die Registrierung ablaufen. Der Eigentümer hat jedoch während des Registrierungszeitraums die Möglichkeit der Verängerung der Registrierung.
 - Der Ersteller des Smart Contracts soll die Möglichkeit haben unsachgemäße Registrierungen zu löschen.

[[section0203]]
=== Smart Contract

Im folgenden soll der entsprechende Smart Contract zur Registrierung von Subdomain Redirects besprochen werden. Dabei werden Ausschnitte des gesamten Smart Contracs einzeln angeführt.

[[solidity0201]]
.Listing 2.1
----
contract SubdomainRedirect {
 
  struct SubdomainEntry {
    string redirect;
    address owner;
    uint registeredUntil;
  }

  uint public registrationFee = 5 finney;
  uint public registrations = 0;
  mapping (string => SubdomainEntry) entries;
  mapping (uint => string) names;

  // ...

}
----

In <<solidity0201>> werden die generischen Smart Contract Attribute aufgeführt. Als wesentlicher Bestandteil ist die Datenstruktur `SubdomainEntry` zu nennen, welche eine Registrierung repräsentiert. Folgende Attribute sind im `SubdomainEntry` enthalten:

 - `redirect` ist eine Zeichenkette, welche den hinterlegten Redirect Link enthält.
 - `owner` repräsentiert den Eigentümer der Registrierung als Ethereum adresse.
 - `registeredUntil` gibt den Zeitstempel an, an dem die Registrierung abläuft.

Im Smart Contract ist des weiteren eine `registrationFee` hinterlegt, welche die Kosten für eine Registrierung angibt. Im Beispiel werden 5 Finneys angegeben. `registrations` ist ein Zähler, welcher die Anzahl von Registrierungen angibt. `entries` enthält alle Registrierungen und sind über den Namen der Subdomain indexiert. Ein weiterer Index `names` ermöglicht die Abfrage des Namens der registrierten Subdomain anhand einer Ganzahl. 

[[solidity0202]]
.Listing 2.2
----
contract SubdomainRedirect {

    // ...
 
    function createRegistration(string name, string redirect) external payable {
        require(
            msg.value >= registrationFee &&
            ( entries[name].owner == address(0) ||
              entries[name].registeredUntil < now ) &&
            bytes(redirect).length > 0
        );

        entries[name].redirect = redirect;
        entries[name].owner = msg.sender;
        entries[name].registeredUntil = now + 1 years + 30 days;
        names[registrations] = name;
        registrations += 1;
    }

  // ...

}
----

Eine Registrierung kann erstellt werden mittels der Funktion `createRegistration`, welche in <<solidity0202>> dargestellt ist. Die Registrierung benötigt zwei Parameter `name` und `redirect`, welche den zu registrierenden Sudomain Namen und andererseits den zu registrierenden Redirect Link spezifizieren.

Im ersten Schritt wir überprüft ob die Registrierung durchgeführt werden kann. Dazu wird überprüft ob die Gebühr enthalten ist und keine gültige Registrierung für den gewünschten Namen vorherscht. Bei einer erfolgreichen Überprüfung wird der Sender der Transaktion als Besitzer der Registrierung angegeben. Weiters wird das Ablaufdatum mit einem Jahr und 30 Tagen gesetzt.

[[solidity0203]]
.Listing 2.3
----
contract SubdomainRedirect {

    // ...
 
    function renewRegistration(string name) external payable {
        require(
            msg.value >= registrationFee &&
            entries[name].owner == msg.sender
        );

        entries[name].registeredUntil = now + 1 years + 30 days;
    }

    function updateRegistration(string name, string redirect) external {
        require(
            entries[name].owner == msg.sender &&
            entries[name].registeredUntil > now
        );

        entries[name].redirect = redirect;
    }

    // ...

}
----

Neben der Erstellung einer neuen Registrierung (siehe <<solidity0202>>) können bestehende Registrierungen (1) erneuert werden bzw. (2) editiert werden. In <<solidity0203>> werden die entsprechenden Funktionen `renewRegistration` und `updateRegistration` dargestellt:

 - Die Erneuerung der Registrierung kann über die Funktion `renewRegistration` durchgeführt werden. Als Parameter benötigt die Funktion den registrierten Namen `name`. Falls die Registrierung bereits für den Sender (`msg.sender`) vorliegt und die entsprechende Gebühr (`msg.value`) enthalten ist, kann die Erneuerung für 1 Jahr und 30 Tage durchgeführt werden.
 - Eine bestehende Registrierung kann über die Funktion `updateRegistration` durchgeführt werden. Dabei sieht die Signatur der Funktion die Parameter `name` und `redirect` vor, welche den Namen der Subdomain bzw. den neuen Redirect Link enthalten sollen. Falls die Registrierung auf den Sender (`msg.sender`) der Transaktion registriert ist, kann die Editierung durchgeführt werden.

[[solidity0204]]
.Listing 2.4
----
contract SubdomainRedirect {

    // ...
 
    function clearRegistration(string name, bool full) ownerOnly external {
        entries[name].redirect = "";

        if (full) {
            entries[name].owner = address(0);
            entries[name].registeredUntil = 0;
        }
    }

    // ...

}
----

Für den Ersteller des Smart Contracts wird die Funktion bereitgestellt eine Registrierung aufzulösen. Dabei besteht die Möglichkeit einer schwachen Auflösung (quasi als Warnung) und einer vollen Auflösung. Die Funktion `clearRegistration` stellt dies bereit. Als Parameter wird der Name der Registrierung (`name`) und die Angabe ob eine volle Auflösung stattfinden soll (`full`).

[[section0204]]
=== Server Anwendung

Die Server Anwendung würde vom Betreiber der Subdomain Registry ausgeführt werden müssen. Die Server Anwendung nimmt Aufrufe entgegen und interpretiert diese entsprechend (Extraktion der Subdomain) und erzeug einen entsprechenden Redirect.

[[listing0205]]
.Listing 2.5
```
var request, response, contract;
var subdomain = '';

if (request.hasSubdomains()) {
    subdomain = request.getSubdomain();
}

var registration = contract.getRegistration(subdomain);

if (registration && now <= registration.registeredUntil) {
    response.redirect(registration.redirect);
} else {
    response.send(404, "No redirect registered");
}
```

In <<listing0205>> ist das Server Programm als einfaches Pseudocode Programm aufgelistet. Es gibt einen `request` an den Server, welcher falls vorhanden die Subdomain abfragen lässt (`request.getSubdomain()`). Der Smart Contract kann ebenfalls über eine Schnittstelle zum Ethereum Netzwerk abgefragt werden. Dies wird über die `contract` Variable beschrieben. So kann der `contract` über die Funktion `getRegistration()` und dem Übergabeparameter `subdomain` abgefragt werden. In der Variable `registration` befinden sich die entsprechenden Informationen zur Registrierung, falls eine vorhanden ist. Falls die Registrierung noch gültig ist (`now <= registration.registeredUntil`) wir ein Redirect (`response.redirect()`) zur hinterlegten Subdomain (`registration.redirect`) initiert. Falls keine Subdomain registriert wurde, wird ein 404 Status an den Client (`Not Found`) zurückgesendet.

[[section0205]]
=== Web-Anwendung

Die Webanwendung zur Registrierung von Subdomain Redirects bietet eine Benutzerschnittstelle um folgende Funktionen auszuführen:

 - Die neuesten Registrierungen werden inklusive wichtiger Daten (zB Ablaufdatum) angezeigt.
 - Es können neue Registrierungen erstellt werden.
 - Die Besitzerin einer Registrierung kann jederzeit den Redirect Link verändern.
 - Die Besitzerin einer Registrierung kann die Registrierung verlängern.

In <<redirect-image1> wird die grundlegende Benutzerschnittstelle dargestellt. Dort befinden sich 3 Buttons um die ensprechenden 3 Funktionen `Erstellung`, `Änderung` bzw. `Verlängerung` von Registierungen auszuführen. Ebenfalls sind die neuesten Registrierungen geordnet nach einem Ablaufdatum ersichtlich.

[[redirect1-image]]
.Subdomain Registry Web Anwendung 1
image::images/REDIRECT_1.png["Subdomain Registry Web Anwendung"]

In <<redirect2-image> ist die Maske zur Erstellung einer neuen Registrierung ersichtlich. Über diese Maske kann ein `Name` bzw. ein `Redirect Link` für die Registrierung festgelegt werden. Falls der `Name` noch verfügbar ist bzw. eine bestehende Registrierung bereits abgelaufen ist, würde eine neue Registrierung erfolgreich sein.

[[redirect2-image]]
.Subdomain Registry Web Anwendung 2
image::images/REDIRECT_2.png["Subdomain Registry Web Anwendung"]

<<redirect3-image>> zeigt die Maske zur Verlängerung einer bestehenden Registrierung. Die Besitzerin der Registrierung kann über die Eingabe des `Namen` der registrierten Subdomain eine Verlängerung durchführen. Dies ist nur möglich falls die Subdomain auch tatsächlich durch sie registriert wurde.

[[redirect3-image]]
.Subdomain Registry Web Anwendung 3
image::images/REDIRECT_3.png["Subdomain Registry Web Anwendung"]

Eine bestehende Registrierung kann von der Besitzerin jederzeit abgeändert werden. Der `Redirect Link` kann somit jederzeit geändert werden. Die Besitzerin muss den `Name` der bestehenden Sudomain Registrierung und zusätzlich einen neuen `Redirect Link` angeben. Die ist in <<redirect4-image>> ersichtlich.

[[redirect4-image]]
.Subdomain Registry Web Anwendung 4
image::images/REDIRECT_4.png["Subdomain Registry Web Anwendung"]

[[section0206]]
=== Diskussion

Die Demonstration zeigt einen Smart Contract zur Subdomain-Registrierung mit zugehöriger Web-Anwendung für Benutzer und einer Serveranwendung für den Betreiber. Der Smart Contract und die Web-Anwendung können potenziell vollständig dezentralisiert werden, sodass der Einfluss des Betreibers minimiert wird. Jedoch muss die Serveranwendung, welche den Redirect durchführt zentral vom Betreiber gehostet werden. Um eine vollständige Dezentralisierung des Szenarios durchzuführen, müsste die Serveranwendung, welche eine Web-Service bereitstellt dezentralisiert werden. Es gibt Projekte (zB [https://dadi.cloud](DADI)) mit dem Ziel Web-Services zu dezentralisieren, eine verlässliche Nutzbarkeit ist jedoch noch ungewiss.
